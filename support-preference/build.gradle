apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion

        consumerProguardFiles 'proguard-consumer-rules.pro'
    }

    resourcePrefix 'asp_'

    sourceSets {
        main {
            res.srcDirs += 'src/main/res-aosp'
            res.srcDirs += 'src/main/res-media'
        }
    }

    lintOptions {
        disable 'ResourceName', 'InlinedApi'
        warning 'GradleCompatible'
        checkReleaseBuilds = false
        abortOnError = false
        // Revert when lint stops with all the false positives >:-(
    }
}

configurations {
    metalava
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'

    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.preference:preference:1.1.0'

    implementation project(':support-spinner')

    // Metalava isn't released yet. Check in its jar and explicitly track its transitive deps.
    metalava rootProject.files('metalava.jar')
    metalava("com.android.tools.external.org-jetbrains:uast:27.2.0-alpha11")
    metalava("com.android.tools.external.com-intellij:kotlin-compiler:27.2.0-alpha11")
    metalava("com.android.tools.external.com-intellij:intellij-core:27.2.0-alpha11")
    metalava("com.android.tools.lint:lint-api:27.2.0-alpha11")
    metalava("com.android.tools.lint:lint-checks:27.2.0-alpha11")
    metalava("com.android.tools.lint:lint-gradle:27.2.0-alpha11")
    metalava("com.android.tools.lint:lint:27.2.0-alpha11")
    metalava("com.android.tools:common:27.2.0-alpha11")
    metalava("com.android.tools:sdk-common:27.2.0-alpha11")
    metalava("com.android.tools:sdklib:27.2.0-alpha11")
    metalava("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.30")
    metalava("org.jetbrains.kotlin:kotlin-reflect:1.4.30")
    metalava("org.ow2.asm:asm:8.0")
    metalava("org.ow2.asm:asm-tree:8.0")
}

repositories {
    jcenter()
    google()
}

group = rootProject.GROUP_ID
version = rootProject.VERSION_NAME

apply from: rootProject.file('android-metalava.gradle')
