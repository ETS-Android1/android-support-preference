apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        checkReleaseBuilds = false
        abortOnError = false
        // Revert when lint stops with all the false positives >:-(
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfigProvider.configure {
            it.enabled = false
        }
    }
}

configurations {
    metalava
}

dependencies {
    implementation "com.android.support:support-annotations:$supportLibVersionAnnotations"

    api project(':support-preference')

    // Metalava isn't released yet. Check in its jar and explicitly track its transitive deps.
    metalava rootProject.files('metalava.jar')
    metalava 'com.android.tools.external.org-jetbrains:uast:26.4.0-beta01'
    metalava 'com.android.tools.external.com-intellij:intellij-core:26.4.0-beta01'
    metalava 'com.android.tools.lint:lint-api:26.4.0-beta01'
    metalava 'com.android.tools.lint:lint-checks:26.4.0-beta01'
    metalava 'com.android.tools.lint:lint-gradle:26.4.0-beta01'
    metalava 'com.android.tools.lint:lint:26.4.0-beta01'
    metalava 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.20'
    metalava 'org.jetbrains.kotlin:kotlin-reflect:1.3.20'
}

repositories {
    jcenter()
    google()
}

group = rootProject.GROUP_ID
version = rootProject.VERSION_NAME

apply from: rootProject.file('android-release.gradle')
apply from: rootProject.file('android-metalava.gradle')
